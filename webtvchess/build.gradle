apply plugin: 'java'
apply plugin: 'war'

group = 'com.alonsoruibal'
archivesBaseName = 'webtvchess'
version = '0.8'
sourceCompatibility = '1.7'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'com.google.gwt:gwt-user:2.5.1'
    compile 'com.google.gwt:gwt-dev:2.5.1'
    compile 'org.vectomatic:lib-gwt-svg:0.5.7'
    compile 'com.alonsoruibal:carballo-core:0.8'
    compile 'com.alonsoruibal:carballo-core:0.8:sources'
    compile 'com.alonsoruibal:carballo-gwt:0.8'
    compile 'com.alonsoruibal:carballo-gwt:0.8:sources'
}

task compileGwt (dependsOn: classes, type: JavaExec) {
    ext.buildDir = "${project.buildDir}/gwt"
    ext.extraDir = "${project.buildDir}/extra"

    inputs.source sourceSets.main.java.srcDirs
    inputs.dir sourceSets.main.output.resourcesDir
    outputs.dir buildDir

    doFirst {
        file(buildDir).mkdirs()
        //classpath.each { println it.name}
    }

    main = 'com.google.gwt.dev.Compiler'

    classpath {
        [
                sourceSets.main.java.srcDirs,           // Java source
                sourceSets.main.output.resourcesDir,    // Generated resources
                sourceSets.main.output.classesDir,      // Generated classes
                sourceSets.main.compileClasspath,       // Deps
        ]
    }

    args =
        [
                'org.vectomatic.libgwtsvgchess',
                '-war', buildDir,
                '-logLevel', 'INFO',
                '-localWorkers', '2',
                '-compileReport',
                '-extra', extraDir,
        ]

    maxHeapSize = '256M'
}

war.dependsOn compileGwt
war {
    from compileGwt.buildDir
}
war.doLast {
    ant.unzip(src: war.archivePath, dest: "./war")
}