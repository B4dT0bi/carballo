apply plugin: 'java'
apply plugin: 'maven'

archivesBaseName = 'carballo-jse'

sourceCompatibility = 1.7
targetCompatibility = 1.7

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:4.9'
    }
}

dependencies {
    compile project(':core')
    testCompile 'junit:junit:4.12'
}

task proguard(type: proguard.gradle.ProGuardTask, dependsOn: 'jar') {
    doFirst {
        configurations.compile.each { println it.name}
    }

    ext {
        outDir = file("./")
        obfuscatedJar = "${outDir}/carballo-${version}.jar"
    }
    outDir.mkdirs()

    injars jar.archivePath
    injars configurations.compile
    outjars obfuscatedJar

    //printmapping "${outDir}/mappings.out"
    //libraryjars configurations.compile
    libraryjars "<java.home>/lib/rt.jar"

    optimizationpasses 5

    allowaccessmodification
    dontskipnonpubliclibraryclassmembers
    keep "public class com.alonsoruibal.chess.uci.Uci { *; }"

    doLast {
        logger.lifecycle "[Proguard] Generated obfuscated JAR in ${obfuscatedJar}"
    }
}

jar {
    manifest.attributes("Main-Class": "com.alonsoruibal.chess.uci.Uci")
}


